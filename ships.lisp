(in-package :yashmup)

;;; Ships
(defclass ship (game-object) 
  ((x-vel :initform 4)
   (y-vel :initform 4)))

(defclass player-ship (ship)
  ((x-loc :initform (/ *screen-width* 2))
   (y-loc :initform (- *screen-height* 100))
   (graphic :initform *player-ship-image*)
   (firing-p :initform nil :accessor firing-p)
   (shots :initform nil :accessor shots)))

(defmethod update ((ship player-ship))
  (with-slots ((x x-loc)
	       (y y-loc)
	       (dx x-vel)
	       (dy y-vel))
      ship
    (when (key-down-p :sdl-key-left)
      (unless (>= -20 x)
       (decf x dx)))
    (when (key-down-p :sdl-key-right)
      (unless (<= (- *screen-width* 30) x)
	(incf x dx)))
    (when (key-down-p :sdl-key-up)
      (unless (>= -30 y)
       (decf y dy)))
    (when (key-down-p :sdl-key-down)
      (unless (<= (- *screen-height* 40) y)
       (incf y dy)))
    (if (key-down-p :sdl-key-space)
	(setf (firing-p ship) t)
	(setf (firing-p ship) nil))))

;;; pew pew
(defclass laser (game-object)
  ((x-vel :initform 0)
   (y-vel :initform -20)
   (graphic :initform *laser-image*)
   (shooter :initarg :shooter :accessor shooter)))

(defmethod update ((laser laser))
  (with-slots ((x x-loc)
	       (y y-loc)
	       (dx x-vel)
	       (dy y-vel))
      laser
    (incf x dx)
    (incf y dy)
    (when (< y -10)
      (setf (shots (shooter laser))
	    (delete laser (shots (shooter laser)))))))